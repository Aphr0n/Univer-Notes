# Розгалудження
- Впливає на порядок виконання інструкцій 
- Типи розгалужень:
	- Умовні
		- beq (branch if equal) 
		- bne (branch if not equal)
	- Безумовні
		- j (jump)
		- jr (jump register)
		- jal (jump and link)
## Інструкції
### beq - branch if equal
```MIPS
addi $s0, $0, 4 ## $s0 = 0 + 4 = 4
addi $s1, $0, 1 ## $s1 = 0 + 1 = 1
sll $s1, $s1, 2 ## $s1 = 1 << 2 = 4
beq $s0, $s1, target ## vykoná sa
addi $s1, $s1, 1 ## nevykoná sa
sub $s1, $s1, $s0 ## nevykoná sa
target: ## návesť
add $s1, $s1, $s0 ## $s1 = 4 + 4 = 8
```

### bne - branch if not equal
```MIPS
addi $s0, $0, 4 ## $s0 = 0 + 4 = 4
addi $s1, $0, 1 ## $s1 = 0 + 1 = 1
sll $s1, $s1, 2 ## $s1 = 1 << 2 = 4
bne $s0, $s1, target ## sa nevykoná
addi $s1, $s1, 1 ## $s1 = 4 + 1 = 5
sub $s1, $s1, $s0 ## $s1 = 5 – 4 = 1
target:
add $s1, $s1, $s0 ## $s1 = 1 + 4 = 5
```

### j
```MIPS
addi $s0, $0, 4 ## $s0 = 4
addi $s1, $0, 1 ## $s1 = 1
j target ## skok na návesť target
sra $s1, $s1, 2 ## nevykoná sa
addi $s1, $s1, 1 ## nevykoná sa
sub $s1, $s1, $s0 ## nevykoná sa
target:
add $s1, $s1, $s0 ## $s1 = 1 + 4 = 5
```

### jr
jr je inštrukcia typu R
```
0x00002000 addi $s0, $0, 0x2010
0x00002004 jr $s0
0x00002008 addi $s1, $0, 1
0x0000200C sra $s1, $s1, 2
0x00002010 lw $s3, 44($s1)
```

## Реалізація команд
### if
```C
if (i == j)
	f = g + h;
f = f – i;
```

```MIPS
## $s0 = f, $s1 = g, $s2 = h
## $s3 = i, $s4 = j
bne $s3, $s4, L1
add $s0, $s1, $s2
L1: sub $s0, $s0, $s3
```
В асемблері умова перевіряється (i != j), на відміну від коду на C (i == j)

### if/else
```C
if (i == j)
	f = g + h;
else
	f = f – i;
```

```
## $s0 = f, $s1 = g, $s2 = h
## $s3 = i, $s4 = j
	bne $s3, $s4, L1
	add $s0, $s1, $s2
	j done
L1: sub $s0, $s0, $s3
done:
```

# Цикли
## while
```C
// determines the power
// of x such that 2x = 128
int pow = 1;
int x = 0;
while (pow != 128) {
	pow = pow * 2;
	x = x + 1;
}
```

```MIPS
# $s0 = pow, $s1 = x
	addi $s0, $0, 1
	add $s1, $0, $0
	addi $t0, $0, 128
while: beq $s0, $t0, done
	sll $s0, $s0, 1
	addi $s1, $s1, 1
	j while
done:
```
В асемблері умова перевіряється (pow == 128), на відміну від коду на C (pow != 128).
## for
for (ініціалізація; умова; операції керування циклом) 
команди
• ініціалізація: виконується перед циклом
• умова: перевіряється перед початком виконання циклу
• операції керування циклом: виконуються після кожного циклу
• оператор(и): виконується, якщо умова циклу виконана

```C
// add the numbers from 0 to 9
int sum = 0;
int i;
for (i=0; i!=10; i = i+1) {
	sum = sum + i;
}
```

```MIPS
# $s0 = i, $s1 = sum
	addi $s1, $0, 0
	add $s0, $0, $0
	addi $t0, $0, 10
for: beq $s0, $t0, done
	add $s1, $s1, $s0
	addi $s0, $s0, 1
	j for
done:
```

## Порівняння
```C
// add the powers of 2 from 1
// to 100
int sum = 0;
int i;
for (i=1; i < 101; i = i*2) {
	sum = sum + i;
}
```

```MIPS
# $s0 = i, $s1 = sum
	addi $s1, $0, 0
	addi $s0, $0, 1
	addi $t0, $0, 101
loop: slt $t1, $s0, $t0
	beq $t1, $0, done
	add $s1, $s1, $s0
	sll $s0, $s0, 1
	j loop
done:
```
`$t1 = 1 if i < 101`
