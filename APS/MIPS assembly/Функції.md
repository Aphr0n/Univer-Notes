Існує 2 типи функцій:
- Викликаюча: у нашому випадку це main()
	- передає аргументи викликаній функції
	- «Переходить» до викликаної функції
- Викликана: у нашому випадку це sum()
	- Виконує функцію
	- Повертає результат обчислення до викликаючої функції
	- Забезпечує повернення до місця виклику
	- Не може перезаписувати вміст регістрів та місць пам'яті, що використовуються викликаючою функцією
```C
void main(){
	int y;
	y = sum(42, 7);
	...
}
int sum(int a, int b){
	return (a + b);
}
```
# Робота з функціями в MIPS
• Виклик функції: інструкція jal (jump and link)
• Повернення функції: інструкція jr (jump register)
• Аргументи: $a0 - $a3
• Повернене значення: $v0
## Виклик функції в MIPS
```C
int main(){
	simple();
	a = b + c;
}

void simple(){
	return;
}
```
```MIPS
0x00400200 main: jal simple
0x00400204 add $s0, $s1, $s2
...
0x00401020 simple: jr $ra
```
- jal: перехід до простої мітки
	$ra = PC + 4 = 0x00400204
- jr $ra: перехід до адреси, що зберігається в $ra (0x00400204)
# Аргументи та повернене значення
Конвенція MIPS:
• Значення аргументів: $a0 - $a3
• Повернене значення: $v0

```C
int main(){
int y;
	...
	y = diffofsums(2, 3, 4, 5); // 4 arguments
	...
}
int diffofsums(int f, int g, int h, int i){
	int result;
	result = (f + g) - (h + i);
	return result; // return value
}
```
```MIPS
# $s0 = y
main:
	...
	addi $a0, $0, 2 # argument 0 = 2
	addi $a1, $0, 3 # argument 1 = 3
	addi $a2, $0, 4 # argument 2 = 4
	addi $a3, $0, 5 # argument 3 = 5
	jal diffofsums # call Function
	add $s0, $v0, $0 # y = returned value
	...
# $s0 = result
diffofsums:
	add $t0, $a0, $a1 # $t0 = f + g
	add $t1, $a2, $a3 # $t1 = h + i
	sub $s0, $t0, $t1 # result = (f + g) - (h + i)
	add $v0, $s0, $0 # put return value in $v0
	jr $ra # return to caller
```

```MIPS
# $s0 = result
diffofsums:
	add $t0, $a0, $a1 # $t0 = f + g
	add $t1, $a2, $a3 # $t1 = h + i
	sub $s0, $t0, $t1 # result = (f + g) - (h + i)
	add $v0, $s0, $0 # put return value in $v0
	jr $ra # return to caller
```
• Функція diffofsums перезаписує вміст 3 регістрів: $t0, $t1, $s0
• Функція diffofsums може використовувати стек для тимчасового зберігання «деяких» значень
# Виклик декількох функцій
```MIPS
proc1:
	addi $sp, $sp, -4 # make space on stack
	sw $ra, 0($sp) # save $ra on stack
	jal proc2
	...
	lw $ra, 0($sp) # restore $s0 from stack
	addi $sp, $sp, 4 # deallocate stack space
	jr $ra # return to caller
```
